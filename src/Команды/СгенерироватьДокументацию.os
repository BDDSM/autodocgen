//////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения.Аргумент("PATH", "", 
						"Каталог исходных файлов конфигурации 1С. 
						|При использовании опции -g (--git) нужно передавать каталог репозитория")
						.Обязательный(Истина);

	КомандаПриложения.Опция("format f", "confluence",	
						"Формат генерации документации. Поддерживается confluence и html")
						.ТПеречисление()
						.Перечисление("confluence", "confluence", "Документация в формате confluence")
						.Перечисление("html", "html", "Документация в формате html");

	КомандаПриложения.Опция("config c", "",
						"Путь к конфигурационному файлу. По умолчанию ищет в каталоге исходных файлов");

	КомандаПриложения.Опция("git g", Ложь,
						"Включает режим обработки изменений репозитория git");

	КомандаПриложения.Опция("errno e", Ложь,
						"Выполняет генерацию даже при наличии ошибок");

КонецПроцедуры

Функция ПолучитьСтруктуруПараметров(Знач КомандаПриложения)
	
	ПараметрыКоманды = Новый Структура("КаталогИсходныхФайлов, НастройкиСтенда, НастройкиАнализаИзменений, ГенерацияПриНаличииОшибок");
	ПараметрыКоманды.ГенерацияПриНаличииОшибок = КомандаПриложения.ЗначениеОпции("errno");
	ПараметрыКоманды.КаталогИсходныхФайлов = ОбщегоНазначения.КаталогИсходныхФайлов(КомандаПриложения);
	ПараметрыКоманды.НастройкиСтенда = ОбщегоНазначения.ПрочитатьНастройкиСтенда(ПараметрыКоманды.КаталогИсходныхФайлов, КомандаПриложения);
	ПараметрыКоманды.НастройкиАнализаИзменений = ОбщегоНазначения.ПрочитатьНастройкиАнализаИзменений(ПараметрыКоманды.НастройкиСтенда, 
													ПараметрыКоманды.КаталогИсходныхФайлов, КомандаПриложения);

	Возврат ПараметрыКоманды;
	
КонецФункции

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	

	ПараметрыКоманды = ПолучитьСтруктуруПараметров(КомандаПриложения);
	
	ТекстОшибки = "";
	Успешно = ВыполнитьГенерациюДокументации(ПараметрыКоманды, ТекстОшибки);
	
	Если НЕ Успешно Тогда
		
		МенеджерПриложения.ПолучитьЛог().Ошибка(ТекстОшибки);
		ЗавершитьРаботу(1);

	КонецЕсли;

КонецПроцедуры

Функция ВыполнитьГенерациюДокументации(ПараметрыКоманды, ТекстОшибки)
	
	КаталогИсходныхФайлов 		= ПараметрыКоманды.КаталогИсходныхФайлов;
	НастройкиСтенда 			= ПараметрыКоманды.НастройкиСтенда;
	НастройкиАнализаИзменений 	= ПараметрыКоманды.НастройкиАнализаИзменений;

	ТекущийКаталогИсходныхФайлов = ОбъединитьПути(КаталогИсходныхФайлов, "src", "configuration");
	ТекущийКаталогИсходныхФайлов = ОбщегоНазначения.ПолноеИмяФайла(ТекущийКаталогИсходныхФайлов);

	Конфигурация = РазборКонфигураций.ЗагрузитьКонфигурацию(ТекущийКаталогИсходныхФайлов);
	Конфигурация.ЗаполнитьПодсистемыОбъектовКонфигурации();
	Конфигурация.НайтиМодули();
	

	ОписаниеКонфигурации = Конфигурация.ОписаниеКонфигурации();

	НастройкиГенератора = Новый Структура;
	
	НастройкиГенератора.Вставить("Парсер", Конфигурация);
	НастройкиГенератора.Вставить("ОписаниеКонфигурации", ОписаниеКонфигурации);
	НастройкиГенератора.Вставить("Модули", ОписаниеКонфигурации.Модули);
	НастройкиГенератора.Вставить("ПодсистемыКонфигурации", ОписаниеКонфигурации.НайтиОбъектыПоТипу("Подсистемы"));
	НастройкиГенератора.Вставить("ОписаниеКонстант", ОписаниеКонфигурации.НайтиОбъектыПоТипу("Константы")); 
	НастройкиГенератора.Вставить("НастройкиАнализаИзменений", НастройкиАнализаИзменений);
	
	ГенераторСодержимого = ОбщегоНазначения.ПолучитьГенераторДокументации(НастройкиСтенда, "json");
	ГенераторДокументации = Новый ГенераторДокументации(ГенераторСодержимого);
	РезультатГенерации = ГенераторДокументации.Сгенерировать(НастройкиГенератора);
	
	ЕстьОшибкиГенерации = Не ПустаяСтрока(РезультатГенерации.ОшибкиГенерации);
	ТекстОшибки = "";

	Если ПараметрыКоманды.ГенерацияПриНаличииОшибок ИЛИ НЕ ЕстьОшибкиГенерации Тогда

		ОшибкиПубликации = ГенераторДокументации.Опубликовать(РезультатГенерации, НастройкиГенератора);
		Если Не ПустаяСтрока(ОшибкиПубликации) Тогда

			ТекстОшибки = СтрШаблон("Публикация документациии завершилась ошибкой: %1", ОшибкиПубликации);

		КонецЕсли;

	КонецЕсли;

	Если ЕстьОшибкиГенерации Тогда

		ТекстОшибки = СтрШаблон("Генерация документациии завершилась ошибкой: %1", РезультатГенерации.ОшибкиГенерации) + Символы.ПС + ТекстОшибки;

	КонецЕсли;

	Возврат ПустаяСтрока(ТекстОшибки);

КонецФункции

// Возвращает имя команды приложения
Функция ИмяКоманды() Экспорт
	
	Возврат "generate";
	
КонецФункции // ИмяКоманды

// Возвращает описание исполняемой команды
Функция КраткоеОписаниеКоманды() Экспорт
	
	Возврат "Выполняет генерацию документации на основании исходных файлов";
	
КонецФункции // ОписаниеКоманды
